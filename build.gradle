project(":test") {
    apply plugin: "java"
    apply plugin: "jacoco"

    sourceSets {
        test {
            java {
                srcDirs = ["src"]
            }
            resources {
                srcDirs = ["assets"]
            }
        }
    }

    dependencies {
        implementation project(":core")
        testImplementation "junit:junit:4.+"
        testImplementation 'org.mockito:mockito-core:5.11.0'
        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }

    task copyAssets(type: Copy) {
        from project(":core").file("assets")
        into "build/assets"
    }

    test {
        dependsOn copyAssets
        systemProperty 'assetsDir', file("$buildDir/assets").absolutePath
        useJUnit {
            includeCategories 'com.eng1.heslingtonhustle.GdxTestRunner'
        }
        testLogging {
            events "passed", "skipped", "failed"
        }
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        sourceSets project(":core").sourceSets.main
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ['**/*Tests.*', '**/GdxTestRunner.*'])
            }))
        }
    }
}
